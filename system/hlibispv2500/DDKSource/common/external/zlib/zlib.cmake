cmake_minimum_required(VERSION 2.8)

find_package(ZLIB REQUIRED) # to have ZLIB_PREFIX and ZLIB_INSTALL_DIR

include(ExternalProject)

set(ZLIB_RELEASE 1.2.7)
set(ZLIB_URL ${ZLIB_PREFIX}/zlib-${ZLIB_RELEASE}.tar.gz)
#set(ZLIB_INSTALL_DIR ${CMAKE_BINARY_DIR}/downloads/install)
set(ZLIB_CMAKE_ARGS -DCMAKE_INSTALL_PREFIX:PATH=${ZLIB_INSTALL_DIR})
set(ZLIB_CMAKE_ARGS ${ZLIB_CMAKE_ARGS} -DSKIP_INSTALL_FILES:BOOL=ON)
set(ZLIB_PATCH_COMMAND "")
set(ZLIB_CMAKE_CACHE_ARGS "")

message(STATUS "external zlib")

if(MSVC)
  if(CMAKE_CONFIGURATION_TYPES)
    foreach(CONFIG_NAME ${CMAKE_CONFIGURATION_TYPES})
      string(TOUPPER ${CONFIG_NAME} CONFIG_NAME)
            
      # Don't force /W4 and /WX (print all warnings and error out on warning)
      foreach(FLAGS_NAME CXX_FLAGS C_FLAGS) #Compiler flags
        STRING(REGEX REPLACE "/W4" "" CMAKE_${FLAGS_NAME}_${CONFIG_NAME} "${CMAKE_${FLAGS_NAME}_${CONFIG_NAME}}")
        STRING(REGEX REPLACE "/WX" "" CMAKE_${FLAGS_NAME}_${CONFIG_NAME} "${CMAKE_${FLAGS_NAME}_${CONFIG_NAME}}")
      endforeach()
    endforeach()
  endif()
  
  # Force /MT and /MTd${ZLIB_INSTALL_DIR}
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_C_FLAGS_DEBUG:STRING=${CMAKE_C_FLAGS_DEBUG})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_C_FLAGS_MINSIZEREL:STRING=${CMAKE_C_FLAGS_MINSIZEREL})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_C_FLAGS_RELEASE:STRING=${CMAKE_C_FLAGS_RELEASE})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_C_FLAGS_RELWITHDEBINFO:STRING=${CMAKE_C_FLAGS_RELWITHDEBINFO})

  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_CXX_FLAGS_DEBUG:STRING=${CMAKE_CXX_FLAGS_DEBUG})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_CXX_FLAGS_MINSIZEREL:STRING=${CMAKE_CXX_FLAGS_MINSIZEREL})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_CXX_FLAGS_RELEASE:STRING=${CMAKE_CXX_FLAGS_RELEASE})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_CXX_FLAGS_RELWITHDEBINFO:STRING=${CMAKE_CXX_FLAGS_RELWITHDEBINFO})
  
  # Force /MACHINE
  set(ZLIB_PATCH_COMMAND cmake -E chdir . ${ZLIB_PREFIX}/patch/win32/CMakeListsPatch.bat)
endif()

if(MINGW)
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DMINGW_PREFIX:STRING=${MINGW_PREFIX})

  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_EXE_LINKER_FLAGS:STRING=${CMAKE_EXE_LINKER_FLAGS})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_SHARED_LINKER_FLAGS:STRING=${CMAKE_SHARED_LINKER_FLAGS})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_SHARED_MODULE_FLAGS:STRING=${CMAKE_MODULE_LINKER_FLAGS})

  set(ZLIB_PATCH_COMMAND cmake -E chdir <SOURCE_DIR> patch -p0 -i ${ZLIB_PREFIX}/patch/mingw/CMakeLists.patch)
endif()
  
if(ANDROID)
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DANDROID_TARGET_PRODUCT:STRING=${ANDROID_TARGET_PRODUCT})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DANDROID_TARGET_OUT:STRING=${ANDROID_TARGET_OUT})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DANDROID_ROOT:STRING=${ANDROID_ROOT})
endif()

if(${CMAKE_SYSTEM_NAME} MATCHES "Linux" OR ${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
  set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -fPIC")

  if(${FORCE_32BIT_BUILD} MATCHES "ON")
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -m32")
    set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DFORCE_32BIT_BUILD:BOOL=ON)
  endif()
  
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_EXE_LINKER_FLAGS:STRING=${CMAKE_EXE_LINKER_FLAGS})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_SHARED_LINKER_FLAGS:STRING=${CMAKE_SHARED_LINKER_FLAGS})

  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS})
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS})

  if(APPLE)
    set(ZLIB_PATCH_COMMAND cmake -E chdir <SOURCE_DIR> patch -p0 -i ${ZLIB_PREFIX}/patch/darwin/CMakeLists.patch)
  endif()  

  if(META)
    set(ZLIB_PATCH_COMMAND cmake -E chdir <SOURCE_DIR> patch -p0 -i ${ZLIB_PREFIX}/patch/meta/CMakeLists.patch)
  endif()
endif()

# Pass toolchain
if(NOT "${CMAKE_TOOLCHAIN_FILE}" STREQUAL "")
  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_TOOLCHAIN_FILE:STRING=${CMAKE_TOOLCHAIN_FILE})
endif()

# Need to set the build type
set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE})

# Pass any non default configuration
if(CMAKE_CONFIGURATION_TYPES)
  foreach(CONFIG_NAME ${CMAKE_CONFIGURATION_TYPES})
    if(NOT "${CONFIG_NAME}" STREQUAL "Debug" AND
       NOT "${CONFIG_NAME}" STREQUAL "Release" AND
       NOT "${CONFIG_NAME}" STREQUAL "MinSizeRel" AND
       NOT "${CONFIG_NAME}" STREQUAL "RelWithDebInfo")

      string(TOUPPER ${CONFIG_NAME} CONFIG_NAME)
      #Set the flags for this configuration
      foreach(FLAGS_NAME CXX_FLAGS C_FLAGS) #Compiler flags
        set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_${FLAGS_NAME}_${CONFIG_NAME}:STRING=${CMAKE_${FLAGS_NAME}_${CONFIG_NAME}})
      endforeach()
      
      foreach(FLAGS_NAME EXE_LINKER_FLAGS MODULE_LINKER_FLAGS SHARED_LINKER_FLAGS) #Linker flags
        set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_${FLAGS_NAME}_${CONFIG_NAME}:STRING=${CMAKE_${FLAGS_NAME}_${CONFIG_NAME}})
      endforeach()
    endif()
  endforeach()

  set(ZLIB_CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS} -DCMAKE_CONFIGURATION_TYPES:STRING=${CMAKE_CONFIGURATION_TYPES})
endif()

if ( CMAKE_VERSION VERSION_GREATER "2.8.3" )
  # ExternalProject_Add has CMAKE_CACHE_ARGS and URL_MD5
  ExternalProject_Add(
    ${ZLIB_NAME}
    URL ${ZLIB_URL}
    URL_MD5 60df6a37c56e7c1366cca812414f7b85
    DOWNLOAD_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib/download
    SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib/source
    BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib
    INSTALL_DIR ${ZLIB_INSTALL_DIR}
    CMAKE_ARGS ${ZLIB_CMAKE_ARGS}
    CMAKE_CACHE_ARGS ${ZLIB_CMAKE_CACHE_ARGS}
    UPDATE_COMMAND ""
    PATCH_COMMAND ${ZLIB_PATCH_COMMAND}
  )
elseif ( CMAKE_VERSION VERSION_GREATER "2.8.1" )
  # ExternalProject_Add has URL_MD5
  # CMAKE_CACHE_ARGS option in ExternalProject_Add is only implemented in
  # cmake starting in version 2.8.4. Use CMAKE_ARGS for now (this limits
  # the max command line to 8k chars on windows).
  set(ZLIB_CMAKE_ARGS ${ZLIB_CMAKE_ARGS} ${ZLIB_CMAKE_CACHE_ARGS})
  
  ExternalProject_Add(
    ${ZLIB_NAME}
    URL ${ZLIB_URL}
    URL_MD5 60df6a37c56e7c1366cca812414f7b85
    DOWNLOAD_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib/download
    SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib/source
    BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib
    INSTALL_DIR ${ZLIB_INSTALL_DIR}
    CMAKE_ARGS ${ZLIB_CMAKE_ARGS}
    UPDATE_COMMAND ""
    PATCH_COMMAND ${ZLIB_PATCH_COMMAND}
  )
else()
  # CMAKE_CACHE_ARGS option in ExternalProject_Add is only implemented in
  # cmake starting in version 2.8.4. Use CMAKE_ARGS for now (this limits
  # the max command line to 8k chars on windows).
  set(ZLIB_CMAKE_ARGS ${ZLIB_CMAKE_ARGS} ${ZLIB_CMAKE_CACHE_ARGS})
  
  ExternalProject_Add(
    ${ZLIB_NAME}
    URL ${ZLIB_URL}
    DOWNLOAD_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib/download
    SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib/source
    BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/libraries/zlib
    INSTALL_DIR ${ZLIB_INSTALL_DIR}
    CMAKE_ARGS ${ZLIB_CMAKE_ARGS}
    UPDATE_COMMAND ""
    PATCH_COMMAND ${ZLIB_PATCH_COMMAND}
  )
endif()

